apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'widen'
version = '2.22.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url 'https://widen.jfrog.io/widen/repo/'
        credentials {
            username System.env.JFROG_USER ?: USER
            password System.env.JFROG_PASS ?: PASSWORD
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username System.env.JFROG_USER ?: USER
                password System.env.JFROG_PASS ?: PASSWORD
            }

            if (project.version.endsWith("-SNAPSHOT")) {
                url 'https://widen.jfrog.io/widen/libs-snapshots-local'
            }
            else {
                url 'https://widen.jfrog.io/widen/libs-releases-local'
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet'  // @see http://stackoverflow.com/questions/9889674/sbt-jetty-and-servlet-3-0
    all*.exclude group: 'javax.servlet', module: 'servlet-api'
    all*.exclude group: 'org.glassfish.jersey.containers', module: 'jersey-container-servlet' // exclude servlet3 auto-deploy
}

dependencies {
    ext.slf4jVersion = '1.7.13'
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4jVersion
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: slf4jVersion

    compile group: 'joda-time', name: 'joda-time', version: '2.1'
    compile group: 'org.apache.tapestry', name: 'tapestry-core', version: '5.3.8'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.5'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    ext.jerseyVersion = '2.22.1'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: jerseyVersion
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: jerseyVersion
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: jerseyVersion

    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
    testCompile group: 'org.eclipse.jetty.aggregate', name: 'jetty-all', version: '9.3.0.v20150612'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.3.0.v20150612'
}

tasks.idea {
    dependsOn = [ cleanIdeaModule, ideaModule ]
}

idea.module.iml {
    beforeMerged { module ->
        module.dependencies.clear()
    }
}
